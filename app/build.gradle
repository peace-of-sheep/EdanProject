apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "androidx.navigation.safeargs"

def getLocalProperty(String propName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propName]
    } else {
        throw new NoSuchFieldException("Not defined property: " + propName)
    }
}

def stringProperty(String value) {
    return "\"$value\""
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "tech.ankainn.edanapplication"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "EDAN_API_URL", stringProperty(getLocalProperty("edan_url")))
        buildConfigField("String", "EDAN_API_USER", stringProperty(getLocalProperty("edan_api_user")))
        buildConfigField("String", "EDAN_API_PASSWORD", stringProperty(getLocalProperty("edan_api_password")))
        buildConfigField("String", "RENIEC_API_URL", stringProperty(getLocalProperty("reniec_url")))
        buildConfigField("String", "GALDOS_API_URL", stringProperty(getLocalProperty("galdos_url")))
    }

    signingConfigs {
        sign_conf {
            keyAlias = getLocalProperty("signing.key.alias")
            keyPassword = getLocalProperty("signing.key.password")

            storeFile = file(getLocalProperty("signing.store.file"))
            storePassword = getLocalProperty("signing.store.password")
        }
    }

    buildTypes {
        release {
            // enable crashlytics on release mode
            manifestPlaceholders = [crashlyticsEnabled: true]

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign_conf
        }
        debug {
            // disable crashlytics on debug mode
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // fragment support
    implementation "androidx.fragment:fragment:1.2.5"

    // material design
    implementation 'com.google.android.material:material:1.1.0'

    // firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
    implementation 'com.google.firebase:firebase-auth:19.2.0'

    // preferences
    implementation "androidx.preference:preference:1.1.0"

    // Timber log
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'

    // json library
    implementation 'com.google.code.gson:gson:2.8.6'

    // Room
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Lifecycle
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // maps and location
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // camerax
    def camerax_version = "1.0.0-beta05"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:1.0.0-alpha12"

    // permission library util
    implementation 'com.karumi:dexter:6.2.0'

    // navigation component
    implementation "androidx.navigation:navigation-fragment:2.3.0"
    implementation "androidx.navigation:navigation-ui:2.3.0"

    // swipe refresh layout
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
}
